{-
	@title: Onda Lending Pool
	@custom: version 1.0
	@author Konstantin Klyuchnikov (knstntn.asuoki.eth)
-}

#include "storage.func";
#include "utils/utils.func";
#include "utils/codes.func";
#include "utils/exitcodes.func";
#include "lib/admin.func";
#include "lib/user-borrow.func";
#include "lib/user-lend-transfer.func";
#include "lib/test-lib.func";
#include "lib/get-methods.func";
#include "lib/algebra.func";
#include "lib/liquidation.func";
#include "lib/tonlink-lib.func";

 
() recv_internal(int my_balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {
	(slice owner_address, slice jetton_wallet_x_address, slice minter_otoken_address, slice jetton_wallet_otoken_address, slice oracle, cell borrow, cell lend, cell algebra) = storage::load_data();
	slice sender_address = utils::parse_sender_address(in_msg_full);
	slice full_msg = in_msg_body; ;; Save full msg
	if(tonlink::is_oracle(sender_address, oracle)){
		(slice who_send, int time, int value, int price, slice all_msg) = tonlink::parse_response(in_msg_body);
		int op = all_msg~load_uint(32);
		int query_id = all_msg~load_uint(64);

		if (op == op::transfer_notification()) {
			if ((query_id == query::newLendCustomToken()) & (equal_slices(who_send, jetton_wallet_x_address))) {
				lend::newLendCustomToken(all_msg, who_send);
				return ();
			}
			if ((query_id == query::deleteLend()) & (equal_slices(who_send, jetton_wallet_otoken_address))) {
				lend::deleteLend(all_msg, who_send, msg_value, query_id);
				return ();
			}
			if ((query_id == query::deleteBorrowCustomToken()) & (equal_slices(who_send, jetton_wallet_x_address))) {
				borrow::deleteBorrowCustomToken(all_msg, price);
				return ();
			}
			if ((query_id == query::liquidationCallCustomToken()) & (equal_slices(who_send, jetton_wallet_x_address))) {
				liquidation::liquidationCallCustomToken(all_msg, price);
				return ();
			}
			return ();
		}
		if (op == op::newLendNativeToken()) {
			lend::newLendNativeToken(all_msg, who_send, value);
			return ();
		}
		if (op == op::newBorrow()) { 
			borrow::newBorrow(all_msg, who_send, price); 
			return ();  
		}
		if (op == op::deleteBorrowNativeToken()) {
			borrow::deleteBorrowNativeToken(all_msg, value, who_send, price);
			return ();
		}
		if (op == op::liquidationCallNativeToken()) {
			liquidation::liquidationCallNativeToken(all_msg, value, who_send, price);
			return ();
		}

	} else {
		int op = in_msg_body~load_uint(32);
		int query_id = in_msg_body~load_uint(64);
		if (op == admin_op::change_admin()) { admin::change_admin(in_msg_body, sender_address); return (); }  
		if (op == admin_op::change_wallet_x()) { admin::change_wallet_x(in_msg_body, sender_address); return ();} 
		if (op == admin_op::change_minter()) { admin::change_minter(in_msg_body, sender_address); return ();}
		if (op == admin_op::change_wallet_o()) { admin::change_wallet_o(in_msg_body, sender_address); return ();}
		if (op == admin_op::configuration()) { algebra::newConfiguration(sender_address, query_id, in_msg_body); return (); }
		
		tonlink::send_request(sender_address, msg_value, full_msg, oracle);
		return ();

	}

	throw(0xffff); 
}  
